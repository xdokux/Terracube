#ifdef CORE_PROFILE
	#include "/prelude/core_profile_core.glsl"
#else
	#include "/prelude/core.glsl"
#endif

#ifdef CLRWL
	#define TEXTURED
	#define TINTED
#endif

#ifdef DEFERRED_IGNORE
	/* RENDERTARGETS: 1,2 */
	#ifdef SHADOWS_ENABLED
		layout(location = 1, component = 1) out uint colortex2;
	#else
		layout(location = 1) out uint colortex2;
	#endif
#else
	/* RENDERTARGETS: 1 */
#endif

#ifdef TRANSLUCENT // Requires `TINTED`
	layout(location = 0) out f16vec4 colortex1;
#else
	layout(location = 0) out f16vec3 colortex1;

	#ifdef CLRWL
		layout(depth_greater) out float gl_FragDepth;
	#elif defined ALPHA_CHECK
		layout(depth_greater) out float gl_FragDepth;

		uniform float alphaTestRef;
	#else
		layout(depth_unchanged) out float gl_FragDepth;
	#endif
#endif

uniform sampler2D gtexture;

in VertexData {
	#ifdef TINTED
		layout(location = 0, component = 0) flat uint tint;
	#endif

	#ifdef TEXTURED
		layout(location = 1, component = 0) vec2 coord;
	#endif
} v;

#include "/lib/srgb.glsl"

#if defined TINTED && !defined TRANSLUCENT
	#include "/lib/un11_11_10.glsl"
#endif

void main() {
	#ifdef TEXTURED
		#ifdef CLRWL
			immut f16vec4 raw_color = f16vec4(texture(gtexture, v.coord));
			vec4 clrwl_color; vec2 _clrwl_light; float _clrwl_ao; vec4 clrwl_overlay_color;
			clrwl_computeFragment(raw_color, clrwl_color, _clrwl_light, _clrwl_ao, clrwl_overlay_color);
			clrwl_color.rgb = mix(clrwl_color.rgb, clrwl_overlay_color.rgb, clrwl_overlay_color.a);
			f16vec4 color = f16vec4(clrwl_color);
			color.rgb = unpack_un11_11_10(v.tint) * linear(color.rgb);

			#ifdef TRANSLUCENT
				colortex1 = color;
			#else
				colortex1 = color.rgb;
			#endif
		#elif defined TINTED
			#ifdef TRANSLUCENT
				immut f16vec4 color = f16vec4(texture(gtexture, v.coord));
				colortex1 = f16vec4(unpackUnorm4x8(v.tint)) * f16vec4(linear(color.rgb), color.a);

				// TODO: Maybe write the ignore flag here too, for the chunk debug lines.
			#else
				#ifdef ALPHA_CHECK
					immut f16vec4 color = f16vec4(texture(gtexture, v.coord));
					if (color.a < float16_t(alphaTestRef)) discard;
				#else
					immut f16vec3 color = f16vec3(texture(gtexture, v.coord).rgb);
				#endif

				colortex1 = unpack_un11_11_10(v.tint) * linear(color.rgb);
			#endif
		#else
			/* // Currently unused.
				#ifdef ALPHA_CHECK
					immut f16vec4 color = f16vec4(texture(gtexture, v.coord));
					if (color.a < float16_t(alphaTestRef)) discard;
				#else
					immut f16vec3 color = f16vec3(texture(gtexture, v.coord).rgb);
				#endif
			*/
			immut f16vec3 color = f16vec3(texture(gtexture, v.coord).rgb);

			colortex1 = linear(color.rgb);
		#endif
	#else // Has to be `TINTED`.
		#ifdef TRANSLUCENT
			colortex1 = f16vec4(unpackUnorm4x8(v.tint));
		#else
			colortex1 = unpack_un11_11_10(v.tint);
		#endif
	#endif

	#ifdef DEFERRED_IGNORE
		colortex2 = colortex2_g_deferred_ignore;
	#endif
}
